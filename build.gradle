plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
    id "org.ajoberstar.grgit" version "1.7.2"
}

group 'me.geek.tom'
String baseVersion = '1.0'

String gitRev = grgit.head().abbreviatedId
version = baseVersion + "-git.${gitRev}"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'net.dv8tion:JDA:4.1.1_165'

    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.1'
    implementation 'org.apache.logging.log4j:log4j-iostreams:2.13.1'

    implementation 'org.kohsuke:github-api:1.113'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "JoinBot",
                "Specification-Vendor": "Tom_The_Geek",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"Tom_The_Geek",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Main-Class": "me.geek.tom.joinbot.JoinBot"
        ])
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "baseVersion", baseVersion
    inputs.property "commit", gitRev

    from (sourceSets.main.resources.srcDirs) {
        include 'version.json'
        expand ([
                'baseVersion': baseVersion,
                'version': project.version,
                'commit': gitRev
        ])
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'version.json'
    }
}

build.finalizedBy('shadowJar')

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.geek.tom'
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
            artifact(shadowJar) {
                classifier "all"
            }
        }
    }

    repositories {
        maven {
            name = "TomTheGeek-Repo"
            url = "http://maven-upload.tomthegeek.ml/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
